; Service Routine IN to input a single character
;
; Patt & Patel Figure 9.12
; x23 IN
; x04A0 IN Trap Service Vector
;
; Read a single character from keyboard device
; Service Routine for Keyboard Input
	        .ORIG	0x04A0
	        ST	R1, SaveR1		; Save the values in the registers
	        ST	R2, SaveR2		; that are used so that they
	        ST	R3, SaveR3		; can be restored before return

;

                LD      R2, Newline 
L1              LDI     R3, DSR                 ; Check DDR -- is it free? 
                BRzp    L1 
                STI     R2, DDR                 ; Move cursor to new clean line

;

                LEA     R1, Prompt              ; Prompt is starting address of prompt string 
Loop            LDR     R0, R1, #0              ; Get next prompt character 
                BRz     Input                   ; Check for end of prompt string 
L2              LDI     R3, DSR 
                BRzp    L2 
                STI     R0, DDR                 ; Write next character of prompt string 
                ADD     R1, R1, #1              ; Increment prompt pointer
                BRnzp   Loop 

; Gave prompt, now get input from user

Input           LDI     R3, KBSR                ; Has a character been typed?
                BRzp    Input   
                LDI     R0, KBDR                ; Load it into R0 to be returned 
L3              LDI     R3, DSR 
                BRzp    L3 
                STI     R0, DDR                 ; Echo input character to the monitor 

;

L4              LDI     R3, DSR 
                BRzp    L4 
                STI     R2, DDR                 ; Move cursor to new clean line 

; Return from trap 

                LD      R1, SaveR1              ; Restore original values in 
                LD      R2, SaveR2              ; registers before return 
                LD      R3, SaveR3 
                RTI 

; some constants

SaveR1          .BLKW   1
SaveR2          .BLKW   1
SaveR3          .BLKW   1
DSR             .FILL   0xFE04                  ; Address of display status register
DDR             .FILL   0xFE06                  ; Address of display data register
KBSR            .FILL   0xFE00                  ; Address of keyboard status register
KBDR            .FILL   0xFE02                  ; Address of keyboard data register
Newline         .FILL   0x000A
Prompt          .STRINGZ "Input a character>"
                .END